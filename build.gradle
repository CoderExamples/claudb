apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.bmuschko.nexus'

group = 'com.github.tonivade'
archivesBaseName = 'tinydb'
version = '0.8.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile 'com.github.tonivade:resp-server:0.5.0'
    compile 'com.github.tonivade:equalizer:0.2.0'
    compile 'net.sf.jopt-simple:jopt-simple:4.9'
    testCompile 'junit:junit:4.12'
    testCompile 'redis.clients:jedis:2.6.3'
    testCompile 'org.mockito:mockito-core:2.2.29'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

compileJava {  
    options.fork = true
    options.forkOptions.executable = "javac"
}

task fatJar(type: Jar) {
    manifest { 
        attributes "Main-Class": "com.github.tonivade.tinydb.Server"
    }
    baseName = project.name + '-all'
    from { 
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    }
    with jar
}

modifyPom {
    project {
        name 'TinyDB'
        description 'TinyDB is a REDIS implementation in Java'
        url 'https://github.com/tonivade/tiny-db'
        inceptionYear '2015'

        scm {
            url 'https://github.com/tonivade/tiny-db'
            connection 'scm:https://github.com/tonivade/tiny-db.git'
            developerConnection 'scm:git://github.com/tonivade/tiny-db.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://github.com/tonivade/tiny-db/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'tonivade'
                name 'Antonio Mu√±oz'
                email 'antoniogmc@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}
