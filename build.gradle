apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.bmuschko.nexus'

group = 'tonivade'
version = '0.4.3-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}


sourceSets {
    main {
        java {
            srcDirs = ['src/main']
        }
    }
    test {
        java {
            srcDirs = ['src/test']
        }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url = 'http://garagemarket.com.es/nexus/content/repositories/releases/'
    }
    maven {
        url = 'http://garagemarket.com.es/nexus/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'tonivade:tiny-server:0.1.1-SNAPSHOT'
    compile 'net.sf.jopt-simple:jopt-simple:4.9'
    compile 'tonivade:equalizer:0.1.2'
    testCompile 'junit:junit:4.12'
    testCompile 'redis.clients:jedis:2.6.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

compileJava {  
    options.fork = true  
    options.forkOptions.executable = "javac"  
}

task fatJar(type: Jar) {
    manifest { 
        attributes "Main-Class": "tonivade.db.Server"
    }
    baseName = project.name + '-all'
    from { 
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    }
    with jar
}

modifyPom {
    project {
        name 'TinyDB'
        description 'TinyDB is a REDIS implementation in Java'
        url 'https://github.com/tonivade/tiny-db'
        inceptionYear '2015'

        scm {
            url 'https://github.com/tonivade/tiny-db'
            connection 'scm:https://github.com/tonivade/tiny-db.git'
            developerConnection 'scm:git://github.com/tonivade/tiny-db.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://github.com/tonivade/tiny-db/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'tonivade'
                name 'Antonio Mu√±oz'
                email 'antoniogmc@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = false
}

nexus {
    sign = false
    repositoryUrl = 'http://garagemarket.com.es/nexus/content/repositories/releases/'
    snapshotRepositoryUrl = 'http://garagemarket.com.es/nexus/content/repositories/snapshots/'
}
