apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'

group = 'com.github.tonivade'
archivesBaseName = 'tinydb'
version = '0.10.0-SNAPSHOT'

configurations {
  codacy
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url = "http://dl.bintray.com/typesafe/maven-releases" 
    }
    maven { 
      url = "https://jitpack.io"
    }
}

dependencies {
    compile 'com.github.tonivade:resp-server:0.7.0-SNAPSHOT'
    compile 'com.github.tonivade:equalizer:0.2.0'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.3'
    compile 'org.luaj:luaj-jse:3.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'redis.clients:jedis:2.9.0'
    testCompile 'org.mockito:mockito-core:2.8.9'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    codacy("com.github.codacy:codacy-coverage-reporter:-SNAPSHOT")
}

compileJava {  
    options.fork = true
    options.forkOptions.executable = "javac"
}

task fatJar(type: Jar) {
    manifest { 
        attributes "Main-Class": "com.github.tonivade.tinydb.Server"
    }
    baseName = project.name + '-all'
    from { 
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    }
    with jar
}

modifyPom {
    project {
        name 'TinyDB'
        description 'TinyDB is a REDIS implementation in Java'
        url 'https://github.com/tonivade/tinydb'
        inceptionYear '2015'

        scm {
            url 'https://github.com/tonivade/tinydb'
            connection 'scm:https://github.com/tonivade/tinydb.git'
            developerConnection 'scm:git://github.com/tonivade/tinydb.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://github.com/tonivade/tinydb/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'tonivade'
                name 'Antonio Mu√±oz'
                email 'antoniogmc@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.runtime
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}
